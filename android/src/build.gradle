buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.0'
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.reddimon.attribution'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        
        versionCode 1
        versionName "1.0.0-beta"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.0'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    
    compileOnly 'com.facebook.react:react-native:+'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

ext {
    PUBLISH_GROUP_ID = 'com.reddimon'
    PUBLISH_VERSION = '1.0.0-beta'
    PUBLISH_ARTIFACT_ID = 'attribution-sdk'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'Attribution SDK'
                description = 'Mobile app attribution tracking SDK for Android'
                url = 'https://github.com/Davidon4/reddimon-attribution-sdk'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'davidon'
                        name = 'Davidon4'
                        email = 'dev@reddimon.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/Davidon4/reddimon-attribution-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/Davidon4/reddimon-attribution-sdk.git'
                    url = 'https://github.com/Davidon4/reddimon-attribution-sdk/tree/main'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}